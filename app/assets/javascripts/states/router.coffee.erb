App.Router = Em.Router.extend
  enableLogging: <%= Rails.env == 'development' %>

  root: Em.Route.create
    index: Em.Route.create
      route: '/'

      enter: (router) ->
        corpusId = 1  # TESTING!!!

        # Loading the corpus also includes metadata categories and values
        corpus = App.Corpus.find(corpusId)
        router.get('selectedCorpusController').initCorpus(corpus)

      #########################
      # Child routes for index
      #########################

      start: Em.Route.create
        route: '/start'

        enter: (router) ->
          # Choose which search interface to display based on the search type
          # for this corpus
          searchType = router.get('selectedCorpusController').get('searchType')
          @newRoute = switch searchType
            when 'cwb' then 'index.home.cwb.regex'

            # Add routes for other search interfaces/engines here as they are
            # implemented, e.g.
            # when 'corpuscle' then 'index.home.corpuscle.simple'
            # when 'annis2' then 'index.home.annis2.simple'

        connectOutlets: (router) ->
          # Using transitionTo in the *enter* method doesn't work, so we have
          # to do it here, although it has nothing to do with connecting
          # outlets...
          router.transitionTo(@newRoute)


      home: Em.Route.create
        route: '/home'

        connectOutlets: (router) ->
          appController = router.get('applicationController')

          appController.connectOutlet('leftSidebar', 'metadataAccordion')
          appController.connectOutlet
            outletName: 'corpusInfo'
            viewClass: App.CorpusInfoView
            controller: router.get('selectedCorpusController')

        ####################################
        # Events for home
        ####################################

        search: (router) ->
          query = router.get('regexSearchController').createQuery()
          search = App.Search.createRecord(query: query)

          router.get('searchesController').pushObject(search)
          App.store.commit()

        ##########################################
        # Child routes for home
        ##########################################

        cwb: Em.Route.create
          route: '/cwb'

          ##############################################
          # Child routes for home.cwb
          ##############################################

          simple: Em.Route.create()

          regex: Em.Route.create
            route: '/regex'

            connectOutlets: (router) ->
              appController = router.get('applicationController')
              appController.connectOutlet('search', 'cwbRegexSearch')
